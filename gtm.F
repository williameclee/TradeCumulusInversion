      SUBROUTINE GTDMSF( M, N, B, D, T, INC, JUMP )
C
C   GENERAL TRIDIAGONAL MULTIPLE SYSTEMS FACTORIZATION
C
C   ON INPUT
C   
C    M    - THE NUMBER OF SYSTEMS TO BE FACTORED
C    N    - THE ORDER OF EACH SYSTEM
C    B    - A MATRIX OF BOTTOM DIAGONALS
C    D    - A MATRIX OF MAIN DIAGONALS
C    T    - A MATRIX OF TOP DIAGONALS
C    INC  - THE MEMORY INCREMENT, AS STORED, BETWEEN DIAGONAL
C           ELEMENTS OF ON THE TRIDIAGONAL SYSTEMS
C    JUMP - THE MEMEORY INCREMENT BETWEEN THE SAME DIAGONAL
C           ELEMENT ON AN ADJACENT LINEAR SYSTEM
C
      INTEGER  M, N, INC, JUMP
      real  B(N), D(N), T(N)
      INTEGER  K, L, L1, L2
C
C   COMPUTE THE FACTORIZATIONS
C
      L1 = 1
      L2 = L1 + (M-1)*JUMP
      DO 20 K=2,N
          L1 = L1 + INC
          L2 = L2 + INC
CDIR$     IVDEP
          DO 10 L=L1,L2,JUMP
              B(L) = B(L)/D(L-INC)
              D(L) = D(L) - B(L)*T(L-INC)
   10     CONTINUE
   20 CONTINUE
      RETURN
      END
      SUBROUTINE GTDMSS( M, N, B, D, T, X, INC, JUMP )
C
C   GENERAL TRIDIAGONAL MULTIPLE SYSTEMS SOLUTION
C
C   ON INPUT
C   
C    M    - THE NUMBER OF SYSTEMS TO BE FACTORED
C    N    - THE ORDER OF EACH SYSTEM
C    B    - A MATRIX OF BOTTOM DIAGONALS: COMPUTED IN GTDMSF
C    D    - A MATRIX OF MAIN DIAGONALS: COMPUTED IN GTDMSF
C    T    - A MATRIX OF TOP DIAGONALS: COMPUTED IN GTDMSF
C    INC  - THE MEMORY INCREMENT, AS STORED, BETWEEN DIAGONAL
C           ELEMENTS OF ON THE TRIDIAGONAL SYSTEMS
C    JUMP - THE MEMEORY INCREMENT BETWEEN THE SAME DIAGONAL
C    X    - CONTAINS RHS OF MATRICES
C
C   ON OUTPUT
C
C    X    - SOLUTION VECTOR
C
      INTEGER  M, N, INC, JUMP
      real  B(N), D(N), T(N), X(N)
      INTEGER  K, L, L1, L2
C
C   DO THE FORWARD SUBSTITUTIONS
C
      L1 = 1
      L2 = L1 + (M-1)*JUMP
      DO 20 K=2,N
          L1 = L1 + INC
          L2 = L2 + INC
CDIR$     IVDEP
          DO 10 L=L1,L2,JUMP
   10     X(L) = X(L) - B(L)*X(L-INC)
   20 CONTINUE
C
C   DO THE BACKWARD SUBSTITUTIONS
C
      DO 30 L=L1,L2,JUMP
   30 X(L) = X(L)/D(L)
      DO 50 K=N-1,1,-1
          L1 = L1 - INC
          L2 = L2 - INC
CDIR$     IVDEP
          DO 40 L=L1,L2,JUMP
   40     X(L) = (X(L) - T(L)*X(L+INC))/D(L)
   50 CONTINUE
      RETURN
      END
